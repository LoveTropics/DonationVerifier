plugins {
	id 'com.github.johnrengelman.shadow' version '8.1.1'
	id 'org.ajoberstar.grgit' version '5.2.0'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'

archivesBaseName = "DonationVerifier"
group = 'com.lovetropics.donations'
description = "LoveTropics Donation Verifier"
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

def repo = grgit.open(dir: project.file('.'))

if (System.getenv('BUILD_NUMBER') != null) {
    version += ".${System.getenv('BUILD_NUMBER')}"
} else {
    version += ".${repo.log().find().abbreviatedId}"
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

jar {
	from('.') {
		include 'mcdrama/**' 
	}
	manifest {
 	   attributes 'Main-Class': 'com.lovetropics.donations.DonationVerifier',
 	              'Implementation-Version': project.version
	}
}

shadowJar {
	configurations = [project.configurations.runtimeClasspath] 
	mergeServiceFiles()
	dependencies {
		exclude "*.html"
		exclude dependency('org.projectlombok:lombok')
	}
}

repositories {
    mavenCentral()
	jcenter() 
	maven {
		url  "https://jitpack.io"
	}
	maven {
		url "https://oss.sonatype.org/content/repositories/snapshots/"
	}
}

dependencies {
    implementation 'com.discord4j.discord4j:discord4j-core:d714d24'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.google.guava:guava:27.1-jre'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    implementation "com.beust:jcommander:1.72"
    implementation 'commons-io:commons-io:2.6'
    
    compileOnly 'org.projectlombok:lombok:1.18.8'
    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}